import plugin.KiwixConfigurationPlugin

buildscript {
  repositories {
    google()
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath Libs.objectbox_gradle_plugin
    classpath Libs.butterknife_gradle_plugin
  }
}

apply plugin: 'com.android.library'
apply plugin: KiwixConfigurationPlugin
apply plugin: 'io.objectbox'
apply plugin: 'com.jakewharton.butterknife'

android {
  publishNonDefault true
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  sourceSets {
    androidTest {
      java.srcDirs += "$rootDir/core/src/sharedTestFunctions/java"
    }
  }
}

private boolean shouldUseLocalVersion() {
  file("./libs").exists()
}

dependencies {

  // use jdk8 java.time backport, as long app < Build.VERSION_CODES.O
  implementation(Libs.threetenabp)

  // Get kiwixlib online if it is not populated locally
  if (!shouldUseLocalVersion()) {
    api(Libs.kiwixlib)
  } else {
    implementation 'com.getkeepsafe.relinker:relinker:1.3.1'
    implementation fileTree(include: ['*.aar'], dir: 'libs')
  }

  // Android Support
  implementation(Libs.cardview)
  implementation(Libs.constraintlayout)


  // Tab indicator
  implementation(Libs.ink_page_indicator)

  // SquiDB
  implementation(Libs.squidb)
  implementation(Libs.squidb_annotations)
  kapt(Libs.squidb_processor)

  // Square
  implementation(Libs.converter_simplexml) {
    exclude group: "xpp3", module: "xpp3"
    exclude group: "stax", module: "stax-api"
    exclude group: "stax", module: "stax"
  }

  // Leak canary
  debugImplementation(Libs.leakcanary_android)

  implementation(Libs.android_arch_lifecycle_extensions)
  implementation(Libs.objectbox_kotlin)
  implementation(Libs.objectbox_rxjava)
}
