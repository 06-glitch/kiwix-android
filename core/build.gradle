buildscript {
  repositories {
    google()
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
    classpath "org.apache.httpcomponents:httpclient-android:4.3.3"
    classpath "com.testdroid:testdroid-api:2.71"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath "io.objectbox:objectbox-gradle-plugin:$objectboxVersion"
    classpath "org.jlleitschuh.gradle:ktlint-gradle:8.2.0"
  }
}

plugins {
  id("checkstyle")
  id("com.github.triplet.play") version("2.4.1")
}
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.objectbox'
apply plugin: 'jacoco-android'
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
  compileSdkVersion 28

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles 'consumer-rules.pro'
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
}

jacoco {
  toolVersion = "0.8.3"
}

tasks.withType(Test) {
  jacoco.includeNoLocationClasses = true
}

ktlint {
  android = true
}

private boolean shouldUseLocalVersion() {
  file("./libs").exists()
}

dependencies {

  // use jdk8 java.time backport, as long app < Build.VERSION_CODES.O
  implementation("com.jakewharton.threetenabp:threetenabp:1.1.1")

  // Get kiwixlib online if it is not populated locally
  if (!shouldUseLocalVersion()) {
    implementation 'org.kiwix.kiwixlib:kiwixlib:8.1.0'
  } else {
    implementation 'com.getkeepsafe.relinker:relinker:1.3.1'
    implementation fileTree(include: ['*.aar'], dir: 'libs')
  }

  // Android Support
  implementation "androidx.appcompat:appcompat:$appCompatVersion"
  implementation "com.google.android.material:material:$materialVersion"
  implementation "androidx.cardview:cardview:$cardViewVersion"
  implementation "androidx.multidex:multidex:$multidexVersion"
  implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
  implementation "androidx.core:core-ktx:1.0.2"
  implementation "androidx.fragment:fragment-ktx:1.0.0"
  implementation "androidx.collection:collection-ktx:1.1.0"

  // Tab indicator
  implementation "com.pacioianu.david:ink-page-indicator:$inkPageIndicatorVersion"

  // Dagger
  compileOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"

  implementation "com.google.dagger:dagger:$daggerVersion"
  implementation "com.google.dagger:dagger-android:$daggerVersion"
  kapt "com.google.dagger:dagger-compiler:$daggerVersion"
  kapt "com.google.dagger:dagger-android-processor:$daggerVersion"


  // SquiDB
  implementation 'com.yahoo.squidb:squidb:2.0.0'
  implementation 'com.yahoo.squidb:squidb-annotations:2.0.0'
  kapt 'com.yahoo.squidb:squidb-processor:2.0.0'

  // Square
  implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
  implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
  implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
  implementation("com.squareup.retrofit2:converter-simplexml:$retrofitVersion") {
    exclude group: "xpp3", module: "xpp3"
    exclude group: "stax", module: "stax-api"
    exclude group: "stax", module: "stax"
  }


  // ButterKnife
  implementation "com.jakewharton:butterknife:$butterKnifeVersion"
  kapt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

  // RxJava
  implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
  implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

  // Leak canary
  debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-alpha-2'

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
  implementation "android.arch.lifecycle:extensions:1.1.1"
  implementation "io.objectbox:objectbox-kotlin:$objectboxVersion"
  implementation "io.objectbox:objectbox-rxjava:$objectboxVersion"
  implementation 'com.google.android.gms:play-services-location:17.0.0'

  implementation "androidx.tonyodev.fetch2:xfetch2:$fetchVersion"
  implementation "androidx.tonyodev.fetch2okhttp:xfetch2okhttp:$fetchVersion"

  testImplementation "org.junit.jupiter:junit-jupiter:5.4.2"
  testImplementation "io.mockk:mockk:1.9"
  testImplementation "org.assertj:assertj-core:3.11.1"
  testImplementation 'com.jraska.livedata:testing-ktx:1.1.0'
  testImplementation 'androidx.arch.core:core-testing:2.0.1'


}
